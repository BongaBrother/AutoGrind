/*
* Created By BongaBrother
*
* Strykers's Gun Shot move.
*
* To use:
*   - Hold L2 then tap the left/right button (towards your opponent) to shoot in that direction. 
*       * Useful when going for the You Found Me! trophy/achievement.
*       * TIP: Keep holding L2 and tap left/right just as stryker begins to holster his gun
*   - Tap R2 any time to cancel the combo
*
* Tested on:
*   PS3
*/
main
{
    if (get_val(PS3_R2))
    {
        stop_all_combos();
    }
    
    if (get_val(PS3_L2))
    {
        if (get_val(PS3_LEFT))
        {
            facing_left();
            disable_interfering_buttons();
            combo_restart(stryker_shoot);
        }
        
        if (get_val(PS3_RIGHT))
        {
            facing_right();
            disable_interfering_buttons();
            combo_restart(stryker_shoot);
        }
    }
    
    if(combo_running(stryker_shoot))
    {
        disable_interfering_buttons();
    }
}

int front = 0;
int back = 0;
function facing_left()
{
    front = PS3_LEFT;
    back = PS3_RIGHT;
}

function facing_right()
{
    front = PS3_RIGHT;
    back = PS3_LEFT;
}

function disable_interfering_buttons()
{
    set_val(PS3_L1, 0);
    set_val(PS3_L2, 0);
    set_val(PS3_R1, 0);
    set_val(PS3_UP, 0);
    set_val(PS3_DOWN, 0);
    set_val(PS3_LEFT, 0);
    set_val(PS3_RIGHT, 0);
    set_val(PS3_TRIANGLE, 0);
}

function stop_all_combos()
{
    combo_stop(stryker_shoot);
}

combo stryker_shoot
{
    set_val(back, 100);
    wait(25);
    
    set_val(front, 100);
    wait(25);
    
    set_val(PS3_SQUARE, 100);
    wait(25);
    
    set_val(PS3_SQUARE, 0);
    wait(300);
}